apply plugin: 'com.android.application'
apply plugin: 'com.android.databinding'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'newrelic'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.0'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.eazyigz.RussiaMediaSearch"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
    }
}

final DAGGER_VERSION = '2.0.1'
final LEAK_CANARY_VERSION = '1.3.2-SNAPSHOT'
final STETHO_VERSION = '1.1.1'
final RUNNER_VERSION = '0.3'
final DEXMAKER_VERSION = '1.2'

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    apt 'com.android.databinding:compiler:1.+'
    compile 'com.android.support:appcompat-v7:23.0.0'
    compile 'com.android.support:design:23.0.0'
    compile 'com.android.support:recyclerview-v7:23.0.0'
    compile 'com.android.support:support-annotations:23.0.0'

    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'io.realm:realm-android:0.83.0-SNAPSHOT'
    compile 'com.google.code.gson:gson:2.3.1'

    // Square libs
    compile 'com.jakewharton:butterknife:7.0.1'
    compile "com.google.dagger:dagger:${DAGGER_VERSION}"
    apt "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"

    // Profiling
    debugCompile "com.squareup.leakcanary:leakcanary-android:${LEAK_CANARY_VERSION}"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${LEAK_CANARY_VERSION}"
    compile "com.facebook.stetho:stetho:${STETHO_VERSION}"
    compile "com.facebook.stetho:stetho-okhttp:${STETHO_VERSION}"
    compile 'com.newrelic.agent.android:android-agent:5.2.1'

    // Testing
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }
    androidTestCompile "com.android.support.test:runner:${RUNNER_VERSION}"
    androidTestCompile "com.android.support.test:rules:${RUNNER_VERSION}"
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2'
    androidTestCompile "com.google.dexmaker:dexmaker:${DEXMAKER_VERSION}"
    androidTestCompile "com.google.dexmaker:dexmaker-mockito:${DEXMAKER_VERSION}"
    androidTestApt "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"

    configurations.all {
        // Currently espresso is dependent on support-annotations:22.2.0
        resolutionStrategy.force 'com.android.support:support-annotations:23.0.0'
    }
}
